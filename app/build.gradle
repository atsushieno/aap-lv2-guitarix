plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    namespace 'org.androidaudioplugin.ports.lv2.guitarix'
    compileSdk = libs.versions.android.compileSdk.get().toInteger()

    defaultConfig {
        applicationId "org.androidaudioplugin.ports.lv2.guitarix"
        minSdk = libs.versions.android.minSdk.get().toInteger()
        versionCode 1
        versionName libs.versions.aap.lv2.get()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                // https://github.com/google/prefab/blob/bccf5a6a75b67add30afbb6d4f7a7c50081d2d86/api/src/main/kotlin/com/google/prefab/api/Android.kt#L243
                arguments "-DANDROID_STL=c++_shared", "-DAAP_ENABLE_ASAN=" + (enable_asan ? "1" : "0")
            }
        }
    }
    ndkVersion libs.versions.ndk.get()

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        prefab true
    }

    packagingOptions{
        if (enable_asan)
            jniLibs.useLegacyPackaging = true
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation libs.aap.lv2
    implementation libs.aap.core
    implementation libs.aap.ui.compose.app
    implementation libs.aap.ui.web
    implementation libs.libcxx.provider
    androidTestImplementation libs.aap.testing
    //  If you want to test aap-core and aap-lv2 locally, switch to these local references
    //  (along with settings.gradle.kts changes)
    /*
    implementation project(":androidaudioplugin-lv2")
    implementation project(":androidaudioplugin")
    implementation project(":androidaudioplugin-ui-compose-app")
    implementation project(":androidaudioplugin-ui-web")
    androidTestImplementation project(":androidaudioplugin-testing")
     */


    testImplementation libs.junit
    androidTestImplementation libs.test.ext.junit
}

